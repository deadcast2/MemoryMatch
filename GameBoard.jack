class GameBoard {
	field int difficulty;
	field Array cards;
	field int cardCount;
	field int selectedCard;
	field int columnBreak;

    constructor GameBoard new(int Adifficulty) {
		let difficulty = Adifficulty;
		let selectedCard = 0;
		
		if (difficulty = 0) {
			let cardCount = 12;
		}

		if (difficulty = 1) {
			let cardCount = 20;
		}

		if (difficulty = 2) {
			let cardCount = 30;
		}

		do drawBorder();
		do createCards();

        return this;
    }

    method void dispose() {
		var int index;
		var Card current;

		let index = 0;

		// Clean up cards
		while (index < cardCount) {
			let current = cards[index];
			do current.dispose();
			let index = index + 1;
		}

		do cards.dispose();
        do Memory.deAlloc(this);

        return;
    }
	
	method void run() {
      	var char key;
		var boolean running;

		let running = true;

		while (running) {
			while (key = 0) {
            	let key = Keyboard.keyPressed();
         	}
			
			do updateSelection(key);

			while (~(key = 0)) {
				let key = Keyboard.keyPressed();
			}
		}

		return;
	}

	method void updateSelection(int key) {
		var Card current;
		var int temp;

		let current = cards[selectedCard];
		do current.unselect();

		// Up arrow
		if (key = 131) { 
			let temp = selectedCard - columnBreak;

			if (temp > -1) {
				let selectedCard = temp;
			}

			// Jump to next row
			if (temp < 0) {
				let selectedCard = cardCount + temp;
			}
		}   

        // Down arrow
		if (key = 133) { 
			let temp = selectedCard + columnBreak;

			if (temp < cardCount) {
				let selectedCard = temp;
			}

			// Jump to next row
			if (temp > (cardCount - 1)) {
				let selectedCard = temp - cardCount;
			}
		}  
        
		// Left arrow
		if (key = 130) { 
			let selectedCard = selectedCard - 1;

			if(selectedCard < 0) {
				let selectedCard = cardCount - 1;
			}
		}   
        
		// Right arrow
		if (key = 132) { 
			let selectedCard = selectedCard + 1;

			if(selectedCard = cardCount) {
				let selectedCard = 0;
			}
		}   

		let current = cards[selectedCard];
		do current.select();

		return;
	}

	method void createCards() {
		var int index, row, col, columnStart;
		var Card current;

		let cards = Array.new(cardCount);
		let index = 0;
		let columnStart = 0;
		let columnBreak = 10;

		// Setup a smaller card layout
		if (difficulty = 0) {
			let columnBreak = 6;
			let columnStart = 2;
		}

		while (index < cardCount) {
			// Go to next line when break point reached
			if (col = columnBreak) {
				let col = 0;
				let row = row + 1;
			}
			
			let cards[index] = Card.new(0, row, col + columnStart);			

			// Select first card by default.
			if (index = 0) {
				let current = cards[index];
				do current.select();
			}

			let index = index + 1;
			let col = col + 1;
		}

		return;
	}

	method void drawBorder() {
		// Top line
		do Screen.drawLine(8, 8, 502, 8);

		// Bottom line
		do Screen.drawLine(8, 248, 504, 248);

		// Left line
		do Screen.drawLine(8, 8, 8, 248);

		// Right line
		do Screen.drawLine(504, 8, 504, 248);
		
		return;
	}
}
